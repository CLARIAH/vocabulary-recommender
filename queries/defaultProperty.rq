prefix dct: <http://purl.org/dc/terms/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
select ?iri ?label ?description ?score {
  {
    select ?iri (sample(?label) as ?label) (sample(?description) as ?description) (sum(?_score) as ?score) {
      {
        { ?iri a owl:DatatypeProperty. } union { ?iri a owl:ObjectProperty. } 
        bind(if(regex(str(?iri),'${term}','i'),1,0) as ?_score)
      } union {
        { ?iri a owl:DatatypeProperty. } union { ?iri a owl:ObjectProperty. }  
        ?iri dct:description|
             rdfs:comment|
             skos:changeNote|
             skos:definition|
             skos:editorialNote|
             skos:example|
             skos:historyNote|
             skos:scopeNote ?description.
        bind(if(regex(str(?description),'${term}','i'),0.8,0) as ?_score)
      } union {
        { ?iri a owl:DatatypeProperty. } union { ?iri a owl:ObjectProperty. }  
        ?iri rdfs:label|
             skos:altLabel|
             skos:prefLabel|
             skos:hiddenLabel ?label.
        bind(if(regex(str(?label),'${term}','i'),1,0) as ?_score)
      }
    }
    group by ?iri
    order by desc(?score)
  }
  filter(?score>0)
}
limit 10