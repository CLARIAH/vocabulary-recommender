prefix dct: <http://purl.org/dc/terms/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
select ?iri ?label ?description ?score {
  {
    select ?iri (sample(?label) as ?label) (sample(?description) as ?description) (sum(?_score) as ?score) {
      {
        { ?iri a owl:DatatypeProperty. } union { ?iri a owl:ObjectProperty. } 
        filter(regex(str(?iri),'${term}','i'))
        bind(1 as ?_score)
      } union {
        { ?iri a owl:DatatypeProperty. } union { ?iri a owl:ObjectProperty. }  
        ?iri dct:description|
             rdfs:comment|
             skos:changeNote|
             skos:definition|
             skos:editorialNote|
             skos:example|
             skos:historyNote|
             skos:scopeNote ?description.
        filter(regex(str(?description),'${term}','i'))
        bind(0.8 as ?_score)
      } union {
        { ?iri a owl:DatatypeProperty. } union { ?iri a owl:ObjectProperty. }  
        ?iri rdfs:label|
             skos:altLabel|
             skos:prefLabel|
             skos:hiddenLabel ?label.
        filter(regex(str(?label),'${term}','i'))
        bind(1 as ?_score)
      }
    }
    group by ?iri
    order by desc(?score)
  }
}
limit 10